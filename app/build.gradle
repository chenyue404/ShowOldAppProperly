plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.chenyue404.showoldappproperly'
    compileSdk 33

    defaultConfig {
        applicationId "com.chenyue404.showoldappproperly"
        minSdk 33
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    File certificationConfFile = file("certification.properties")
    Properties certificationConf = new Properties()
    certificationConf.load(new FileInputStream(certificationConfFile))

    signingConfigs {
        my {
            storeFile file(certificationConf['storeFile'])
            storePassword certificationConf['storePassword']
            keyAlias certificationConf['keyAlias']
            keyPassword certificationConf['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.my
        }
        debug {
            signingConfig signingConfigs.my
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                def fileName = "ShowOldAppProperly_${defaultConfig.versionCode}_${defaultConfig.versionName}.apk"
                outputFileName = fileName
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'
    compileOnly 'de.robv.android.xposed:api:82'
}